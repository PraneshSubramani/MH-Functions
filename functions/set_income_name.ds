void automation.set_income_name(Int income_id)
{
// Original author - Sam Prabhu
// Contributing developers:
// - Dash Bunyan
//
// @input.income_id - The record ID for the income
//
// assign new name
income_name_list = List();
// get the income record
income_info = zoho.crm.getRecordById("Incomes",income_id);
// get the index of the record
auto_index = income_info.get("Index");
income_name_list.add("INC" + auto_index);
// get the income-contact record
income_contact_info = income_info.get("Contact");
if(!income_contact_info.isNull())
{
    id = income_contact_info.get("id");
    contact_info = zoho.crm.getRecordById("Contacts",id);
    first_name_initial = "";
    first_name = contact_info.get("First_Name");
    last_name = contact_info.get("Last_Name");
    if(!first_name.isNull())
    {
        first_name_initial = first_name.left(1) + ".";
    }
    income_name_list.add(first_name_initial + last_name);
}
// get the type field
type = income_info.get("Type");
if(!type.isNull())
{
    income_name_list.add(type);
}
// get the frequency field of the record
frequency = income_info.get("Frequency");
if(!frequency.isNull())
{
    income_name_list.add(frequency);
}
// assign new name
new_income_name = income_name_list.toListString().replaceAll(","," - ");
payload = Map();
payload.put("Name",new_income_name);
// update the Income
update_result = zoho.crm.updateRecord("Incomes",income_id,payload);
info update_result;
}