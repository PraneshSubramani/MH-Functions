string standalone.sync_document_request(Int document_request_id)
{
// Original author - Sam Prabhu
// Contributing developers:
// - Dash Bunyan
//
// @input.document_request_id - The record ID for the document request.
//
// set constants
INCOMPLETE_TRANSITION = "Stall";
REQUEST_TRANSITION = "Request";
//
// prepare result
result = Map();
//
// get the integration settings
settings = standalone.get_workdrive_integration_settings();
template_settings = standalone.get_template_settings();
//
// get the document request record
document_request = zoho.crm.getRecordById("Document_Requests",document_request_id);
//
// setup the document request folder
folder = standalone.sync_workdrive_folder(document_request_id,"Document_Requests");
parent_folder_id = IFNULL(folder.get("data"),Map()).get("id");
//
// build the document name
// NOTE: We do this here in case the name script hasnt triggered yet for the record.
record_ref = standalone.document_request_build_name(document_request_id);
// 
// get the sender user and reply to for the request
owner = document_request.get("Owner");
user_name = owner.get("name");
user_email = IFNULL(document_request.get("Sender_Email"),owner.get("email"));
reply_to_email = IFNULL(document_request.get("Reply_To"),user_email);
//
// get the recipient contact for the request
contact_id = IFNULL(document_request.get("Contact"),Map()).get("id");
if(contact_id.isNull())
{
    // update details of error on the record
    user_message = "The associated contact must be set.";
    payload = {"Status_Message":user_message,"Request_Result":document_request.toString()};
    transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,INCOMPLETE_TRANSITION,payload);
    //
    // return result
    result.put("error","Failed to create a new collection link for Document Request with ID " + document_request_id + ".");
    result.put("message",payload);
    return result;
}
contact = zoho.crm.getRecordById("Contacts",contact_id);
contact_name = contact.get("Full_Name");
contact_email = document_request.get("Email");
// contact_email = contact.get("Email");
//
// prepare the deadline
deadline = document_request.get("Deadline");
if(deadline.isNull())
{
    // update details of error on the record
    user_message = "A deadline must be given.";
    payload = {"Status_Message":user_message,"Request_Result":document_request.toString()};
    transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,INCOMPLETE_TRANSITION,payload);
    //
    // return result
    result.put("error","Failed to create a new collection link for Document Request with ID " + document_request_id + ".");
    result.put("message",payload);
    return result;
}
deadline = deadline.replaceAll("T"," ").toString("yyyy-MM-dd hh:mm");
//
// prepare the request note
request_note = document_request.get("Request_Note");
//
// check for an existing collection link
collection_id = document_request.get("Link_ID");
external_link = document_request.get("Link");
if(collection_id.isNull() || external_link.isNull())
{
    collection_response = standalone.create_workdrive_collection_link(record_ref,parent_folder_id,deadline,request_note);
    collection_error = collection_response.get("error");
    if(!collection_error.isNull())
    {
        // update details of error on the record
        user_message = "Failed to generate a collection link.";
        payload = {"Status_Message":user_message,"Request_Result":collection_response};
        transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,INCOMPLETE_TRANSITION,payload);
        //
        // return result
        result.put("error","Failed to create a new collection link for Document Request with ID " + document_request_id + ".");
        result.put("message",collection_response);
        return result;
    }
    result.put("collection_link",collection_response.toMap());
    //
    // get the result details
    collection_id = standalone.extract("data/id",collection_response);
    external_link = standalone.extract("data/attributes/link",collection_response);
    //
    // set the details of the collection link on the Document Request record in CRM
    document_request_payload = Map();
    document_request_payload.put("Link_ID",collection_id);
    document_request_payload.put("Link",external_link);
    update_response = zoho.crm.updateRecord("Document_Requests",document_request_id,document_request_payload);
    update_error = update_response.get("error");
    if(!update_error.isNull())
    {
        // update details of error on the record
        user_message = "Failed to save the collection link.";
        payload = {"Status_Message":user_message,"Request_Result":update_response};
        transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,INCOMPLETE_TRANSITION,payload);
        //
        // return result
        result.put("error","Failed to update Document Request with ID " + document_request_id + " to set link " + external_link + ".");
        result.put("message",update_response);
        return result;
    }
    result.put("crm_initial_update",update_response.toMap());
}
else
{
    collection_response = standalone.update_workdrive_collection_link(collection_id,deadline,request_note);
    collection_error = collection_response.get("error");
    if(!collection_error.isNull())
    {
        // update details of error on the record
        user_message = "Failed to update the collection link.";
        payload = {"Status_Message":user_message,"Request_Result":collection_response};
        transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,INCOMPLETE_TRANSITION,payload);
        //
        // return result
        result.put("error","Failed to update collection link for Document Request with ID " + document_request_id + ".");
        result.put("message",collection_response);
        return result;
    }
    result.put("collection_link",collection_response.toMap());
}
//
// prepare the sender and recipients for the notification
from = {"name":user_name,"email":user_email};
recipient = {"name":contact_name,"email":contact_email};
reply_to = {"name":user_name,"email":reply_to_email};
to = {recipient};
if(contact_email.isNull())
{
    // update details of error on the record
    user_message = "A Contact Email must be given.";
    payload = {"Status_Message":user_message,"Request_Result":document_request.toString()};
    transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,INCOMPLETE_TRANSITION,payload);
    //
    // return result
    result.put("error","Failed to send email notification for Document Request with ID " + document_request_id + ".");
    result.put("message","Recipient email is empty");
    return result;
}
//
// send the email notification
template_id = template_settings.get("email_templates").get("request_document");
email_response = standalone.send_crm_email(from,to,reply_to,document_request_id,"Document_Requests",template_id);
email_error = email_response.get("error");
if(!email_error.isNull())
{
    // update details of error on the record
    user_message = "Failed to send the email to the recipient.";
    payload = {"Status_Message":user_message,"Request_Result":email_response};
    transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,INCOMPLETE_TRANSITION,payload);
    //
    // return result
    result.put("error","Failed to send email notification for Document Request with ID " + document_request_id + ".");
    result.put("message",email_response);
    return result;
}
result.put("notification",email_response.toMap());
//
// update the record with the requested date
user_message = "The collection link has been sent.";
requested_time = zoho.currenttime.toString("yyyy-MM-dd'T'hh:mm:ss+00:00");
payload = {"Status_Message":user_message,"Request_Result":collection_response,"Requested":requested_time};
transition_response = standalone.make_blueprint_transition("Document_Requests",document_request_id,REQUEST_TRANSITION,payload);
result.put("crm_update",transition_response.toMap());
//
// return the result
return result;
}